apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion Integer.parseInt(project.compileSdkVersion)
    defaultConfig {
        versionName project.version
        minSdkVersion project.minSdkVersion.toInteger()
        targetSdkVersion project.targetSdkVersion.toInteger()
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    testImplementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:none', true)
}

artifacts {
//    archives javadocJar
    archives sourcesJar
}

apply from: rootProject.file('deploy.gradle')